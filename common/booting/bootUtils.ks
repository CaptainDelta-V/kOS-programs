DECLARE GLOBAL ALTERNATE_BOOT_INDICATOR_FILE TO "1:ALT_BOOT.txt".
LOCAL BOOT_INDICATOR_FILE_PARAM_SEPARATOR TO ",".

GLOBAL FUNCTION SET_ALTERNATE_BOOT_FILE {     
    PARAMETER META_BOOT_FILE.
    PARAMETER BOOT_PARAM_0 IS "".
    PARAMETER BOOT_PARAM_1 IS "".
    PARAMETER BOOT_PARAM_2 IS "".

    DELETEPATH(ALTERNATE_BOOT_INDICATOR_FILE).
    CREATE(ALTERNATE_BOOT_INDICATOR_FILE).

    LOCAL BOOT_PARAM_LIST TO LIST(META_BOOT_FILE, BOOT_PARAM_0, BOOT_PARAM_1, BOOT_PARAM_2).
    LOCAL BOOT_INDICATOR_FILE_CONTENT TO BOOT_PARAM_LIST:JOIN(BOOT_INDICATOR_FILE_PARAM_SEPARATOR).

    OPEN(ALTERNATE_BOOT_INDICATOR_FILE):WRITE(BOOT_INDICATOR_FILE_CONTENT).
}

GLOBAL FUNCTION CHECK_ALT_BOOT_FILE { 
    IF EXISTS(ALTERNATE_BOOT_INDICATOR_FILE) { 
        LOCAL BOOT_INDICATOR_FILE_CONTENT TO OPEN(ALTERNATE_BOOT_INDICATOR_FILE):READALL():STRING.
        
        LOCAL BOOT_PARAM_LIST TO BOOT_INDICATOR_FILE_CONTENT:SPLIT(BOOT_INDICATOR_FILE_PARAM_SEPARATOR).
        LOCAL ALT_BOOT_FILENAME TO BOOT_PARAM_LIST[0].

        IF (BOOT_PARAM_LIST:LENGTH = 4) { 
            RUN_WITH_PARAMS(ALT_BOOT_FILENAME, BOOT_PARAM_LIST[1], BOOT_PARAM_LIST[2], BOOT_PARAM_LIST[3]).
        }
        ELSE IF (BOOT_PARAM_LIST:LENGTH = 3) { 
            RUN_WITH_PARAMS(ALT_BOOT_FILENAME, BOOT_PARAM_LIST[1], BOOT_PARAM_LIST[2]).
        }
        ELSE IF (BOOT_PARAM_LIST:LENGTH = 2) { 
            RUN_WITH_PARAMS(ALT_BOOT_FILENAME, BOOT_PARAM_LIST[1]).
        }
        ELSE { 
            RUNPATH(ALT_BOOT_FILENAME).
        }        
    }
}

GLOBAL FUNCTION RUN_WITH_PARAMS {
    PARAMETER FILENAME. 
    PARAMETER P0 IS "".
    PARAMETER P1 IS "". 
    PARAMETER P2 IS "".

    IF (P2:LENGTH > 0) { 
        RUNPATH(FILENAME, P0, P1, P2).
    }
    ELSE IF (P1:LENGTH > 0) { 
        RUNPATH(FILENAME, P0, P1).
    }
    ELSE IF (P0:LENGTH > 0) { 
        RUNPATH(FILENAME, P0).
    }
    ELSE { 
        RUNPATH(FILENAME).
    }
}

