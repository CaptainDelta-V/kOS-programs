
FUNCTION LANDING_BURN_MODEL { 

    PARAMETER RADAR_OFFSET.

    LOCAL _RADAR_OFFSET TO RADAR_OFFSET.

    LOCK _TRUE_RADAR TO ALT:RADAR - _RADAR_OFFSET.
    LOCK _G TO CONSTANT:G * BODY:MASS / BODY:RADIUS^2.			
    LOCK _MAX_THRUST_ACTUAL TO SHIP:AVAILABLETHRUST.
    LOCK _MAX_DECEL TO (_MAX_THRUST_ACTUAL / SHIP:MASS) - _G.	
    LOCK _VELOCITY_MAG TO SHIP:VELOCITY:SURFACE:MAG.
    LOCK _STOP_DISTANCE TO _VELOCITY_MAG^2 / (2 * _MAX_DECEL).		
    LOCK _IDEAL_THROTTLE TO _STOP_DISTANCE / _TRUE_RADAR.					
    LOCK _IMPACT_TIME TO _TRUE_RADAR / ABS(SHIP:VERTICALSPEED).	

    FUNCTION SET_RADAR_OFFSET { 
        PARAMETER OFFSET. 

        SET _RADAR_OFFSET TO OFFSET.
    }

    FUNCTION GET_RADAR_OFFSET { 
        RETURN _RADAR_OFFSET.
    }

    FUNCTION TRUE_RADAR { 
        RETURN _TRUE_RADAR.
    }

    FUNCTION GRAVITY { 
        RETURN _G.
    }

    FUNCTION MAX_THRUST_ACTUAL { 
        RETURN SHIP:AVAILABLETHRUST.
    }

    FUNCTION MAX_DECEL { 
        RETURN _MAX_DECEL.
    }

    FUNCTION VELOCITY_MAGNITUDE { 
        RETURN _VELOCITY_MAG.
    }

    FUNCTION STOP_DISTANCE { 
        RETURN _STOP_DISTANCE.
    }

    FUNCTION IDEAL_THROTTLE { 
        RETURN _IDEAL_THROTTLE.
    }

    FUNCTION IMPACT_TIME { 
        RETURN _IMPACT_TIME.
    }
    
    RETURN LEXICON( 
        "SET_RADAR_OFFSET", SET_RADAR_OFFSET@,
        "GET_RADAR_OFFSET", GET_RADAR_OFFSET@,
        "TRUE_RADAR", TRUE_RADAR@,
        "GRAVITY", GRAVITY@, 
        "MAX_THRUST_ACTUAL", MAX_THRUST_ACTUAL@,
        "MAXDECEL", MAX_DECEL@,
        "VELOCITY_MAGNITUDE", VELOCITY_MAGNITUDE@,
        "STOP_DISTANCE", STOP_DISTANCE@, 
        "IDEAL_THROTTLE", IDEAL_THROTTLE@,
        "IMPACT_TIME", IMPACT_TIME@
    ).
}