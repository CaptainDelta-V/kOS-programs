@LAZYGLOBAL OFF.
WAIT UNTIL SHIP:UNPACKED.
RUNONCEPATH("constants").
RUNONCEPATH("../../../common/landing/sites").
RUNONCEPATH("../../../common/infos").
RUNONCEPATH("../../../common/engineManager").
RUNONCEPATH("../../../common/flightStatus/flightStatusModel").
RUNONCEPATH("../../../common/control").
RUNONCEPATH("../../../common/nav").
RUNONCEPATH("../../../common/launch/ascentModel").
RUNONCEPATH("../../../common/booting/bootUtils").

CLEARSCREEN.

LOCAL REQUIRED_APOAPSIS_ETA_MARGIN TO 60 * 3.5.
SET SHIP:NAME TO ACTIVE_STARSHIP_VESSEL_NAME.

LOCAL ASCENT TO ASCENT_MODEL().
LOCAL FLIGHT_STATUS TO FLIGHT_STATUS_MODEL("STARSHIP ORBITAL ASCENT CONTROL","UNKNOWN").

FLIGHT_STATUS:ADD_FIELD("ETA APOAPSIS", ASCENT:TIME_TO_APOAPSIS@).
FLIGHT_STATUS:ADD_FIELD("REQUIRED TIME MARGIN", REQUIRED_APOAPSIS_ETA_MARGIN).

LOCAL TARGET_PITCH TO 0.5.
LOCAL TARGET_ROLL TO 180.

RUN_FLIGHT_STATUS_SCREEN(FLIGHT_STATUS, 0.75).

IF SHIP:ORBIT:ETA:APOAPSIS > REQUIRED_APOAPSIS_ETA_MARGIN {    
    FLIGHT_STATUS:UPDATE("ORBIT: IDLE").
    SET CORE:BOOTFILENAME TO "".
}
ELSE { 
   ASCEND_TO_ORBIT().
}

WAIT UNTIL FALSE.

FUNCTION ASCEND_TO_ORBIT { 
    RCS ON.
    RESET_TORQUE(). 
    SET STEERINGMANAGER:YAWTORQUEFACTOR TO 0.5.
    SET STEERINGMANAGER:PITCHTORQUEFACTOR TO 0.5.
    SET STEERINGMANAGER:ROLLTORQUEFACTOR TO 0.5.

    LOCK THROTTLE TO 1.    
    LOCK TARGET_HEADING TO HEADING_OF_VECTOR(SHIP:VELOCITY:ORBIT).
    LOCK STEERING TO HEADING(TARGET_HEADING, TARGET_PITCH, TARGET_ROLL).        
    
    FLIGHT_STATUS:UPDATE("ASCENT").        

    WAIT 4.
    LOCAL BOOSTER TO VESSEL(ACTIVE_STARSHIP_BOOSTER_VESSEL_NAME).         
    FLIGHT_STATUS:ADD_FIELD("BOOSTER TIME TO APOAPSIS", { RETURN BOOSTER:ORBIT:ETA:APOAPSIS.}).
    FLIGHT_STATUS:ADD_FIELD("BOOSTER CONNECTION", { RETURN BOOSTER:CONNECTION:ISCONNECTED. }).

    WHEN ASCENT:TIME_TO_APOAPSIS() > REQUIRED_APOAPSIS_ETA_MARGIN - 30 THEN { 
        BOOSTER:CONNECTION:SENDMESSAGE(INITIATE_LANDING_SEQUENCE_MESSAGE).
        FLIGHT_STATUS:UPDATE("ORBIT: SENDING BOOSTER LAND MESSAGE").
    }

    WHEN ASCENT:TIME_TO_APOAPSIS() > REQUIRED_APOAPSIS_ETA_MARGIN THEN { 
        LOCK THROTTLE TO 0.
        FLIGHT_STATUS:UPDATE("ORBIT: PREPARING TO SWITCH TO BOOSTER").
        SET CORE:BOOTFILENAME TO "".         
        
        WAIT 1.
                   
        SET KUNIVERSE:ACTIVEVESSEL TO VESSEL(ACTIVE_STARSHIP_BOOSTER_VESSEL_NAME).
    }
}
