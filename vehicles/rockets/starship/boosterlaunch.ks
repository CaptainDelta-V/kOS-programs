@LAZYGLOBAL OFF.
WAIT UNTIL SHIP:UNPACKED.
RUNONCEPATH("../../../common/constants").
RUNONCEPATH("../../../common/landing/sites").
RUNONCEPATH("../../../common/landing/landingStatusModel"). 
RUNONCEPATH("../../../common/flightStatus/flightStatusModel").
RUNONCEPATH("../../../common/infos").
RUNONCEPATH("../../../common/control").
RUNONCEPATH("../../../common/nav").
RUNONCEPATH("../../../common/booting/bootUtils").
RUNONCEPATH("../../../common/engineManager").
RUNONCEPATH("../../../common/launch/boosterLaunchModel").
RUNONCEPATH("../../../common/launch/utils").
RUNONCEPATH("constants").

CLEARSCREEN. 

LOCAL ENGINE TO SHIP:PARTSTAGGED("BOOSTER_RAPTORS")[0].
LOCAL ENGINE_MODULE TO ENGINE:GETMODULE(TUNDRA_ENGINE_MODULE_NAME).
LOCAL ENGINE_MANAGEMENT TO ENGINE_MANAGER(ENGINE_MODULE, VESSEL_TYPE_SUPER_HEAVY_BOOSTER).

LOCAL STARTOWER_CORE TO PROCESSOR(TOWER_CPU_NAME).
LOCAL STARSHIP_CORE TO PROCESSOR(STARSHIP_CPU_NAME).

LOCAL BOOSTER_LAUNCH TO BOOSTER_LAUNCH_MODEL(1.4, 11, 8, 16).
LOCAL LAUNCH_HDG TO 90.
LOCAL TARGET_APOAPSIS TO 70_000.
LOCAL TOWER_ALTITUDE TO 143.

LOCK PITCH_TARGET TO BOOSTER_LAUNCH:PITCH_TARGET().
LOCAL FLIGHT_STATUS TO FLIGHT_STATUS_MODEL("SUPER HEAVY BOOSTER LAUNCH CONTROL", "PRELAUNCH").
FLIGHT_STATUS:ADD_FIELD("TARGET PITCH", BOOSTER_LAUNCH:PITCH_TARGET@).
FLIGHT_STATUS:ADD_FIELD("DYNAMIC PRESSURE", BOOSTER_LAUNCH:DYNAMIC_PRESSURE@).
FLIGHT_STATUS:ADD_FIELD("ALT SCALED", BOOSTER_LAUNCH:ALTITUDE_SCALED@).

GET_LAUNCH_CONFIRMATION(FLIGHT_STATUS:GET_TITLE()).
RUN_FLIGHT_STATUS_SCREEN(FLIGHT_STATUS, 0.75).

WAIT 1.
RCS OFF.
SAS OFF.
LOCK THROTTLE TO 0.

IF (STARTOWER_CORE:CONNECTION:SENDMESSAGE(TOWER_DELUGE_START_MESSAGE)) { 
    FLIGHT_STATUS:UPDATE("WATER DELUGE ACTIVATION").    
}
ELSE { 
    FLIGHT_STATUS:UPDATE("WATER DELUGE ACTIVATION FAILURE").
    WAIT 100.
}
WAIT 3.

FLIGHT_STATUS:UPDATE("PRE-IGNITION").
WAIT 0.125.
LOCK THROTTLE TO 1.
ENGINE_MANAGEMENT:SET_ENGINE_STATE(TRUE).
WAIT 0.25.

IF (STARTOWER_CORE:CONNECTION:SENDMESSAGE(TOWER_RELEASE_MESSAGE)) { 
    FLIGHT_STATUS:UPDATE("REQUESTING TOWER RELEASE").
}
ELSE { 
    FLIGHT_STATUS:UPDATE("TOWER RELEASE FAILURE").
    WAIT 100.
}

WAIT 1.
FLIGHT_STATUS:UPDATE("LIFTOFF").

LOCK STEERING TO HEADING(LAUNCH_HDG, PITCH_TARGET, 180). 
WAIT 4.

FLIGHT_STATUS:UPDATE("ASCENT").

WHEN APOAPSIS > TARGET_APOAPSIS THEN { 
    SET FLIGHT_STATUS TO "SHIP SEPARATION".

    UNLOCK THROTTLE.
    UNLOCK STEERING.   
    WAIT 0.
    SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.6.

    ENGINE_MANAGEMENT:SET_ENGINE_MODE(ENG_MODE_MID_INR).
    WAIT 0. 
    ENGINE_MANAGEMENT:SET_ENGINE_MODE(ENG_MODE_CTR).        
    WAIT 1.                
    STARSHIP_CORE:CONNECTION:SENDMESSAGE(STARSHIP_ASCENT_HANDOFF_MESSAGE).    
    WAIT 0.
        
    SET_ALTERNATE_BOOT_FILE("boosterland").    
    WAIT 2.      
    REBOOT.       
}

WAIT UNTIL FALSE.
